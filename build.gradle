buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id "de.marcphilipp.nexus-publish" version "0.3.0"
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java']
    }
    resources {
      srcDirs = ['src/main/resources']
    }
  }
  test {
    java {
      srcDirs = ['src/test/java']
    }
    resources {
      srcDirs = ['src/test/resources']
    }
  }
}

dependencies {
  // This dependency is exported to consumers, that is to say found on their compile classpath.
  compile "org.apache.commons:commons-lang3:latest.release"
  compile "com.google.guava:guava:latest.release"

  compile "com.fasterxml.jackson.core:jackson-core:latest.release"
  compile "com.fasterxml.woodstox:woodstox-core:latest.release"

  // Use JUnit test framework
  testImplementation 'junit:junit:4.12'
}

sourceCompatibility = JavaVersion.VERSION_14
group = "com.vikmad"
archivesBaseName = "base"
version = "0.1.2"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = 'com.vikmad'
        name = 'base'
        description = 'Base algos, data structures and utilities'
        url = 'https://github.com/vikashmadhow/base'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          url = 'https://github.com/vikashmadhow/base'
          connection = 'scm:https://github.com/vikashmadhow/base.git'
          developerConnection = 'scm:git://github.com/vikashmadhow/base.git'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
